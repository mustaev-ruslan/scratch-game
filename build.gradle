buildscript {
    ext {
        lombokVersion = '1.18.26'
        slf4jApiVersion = '2.0.13'
        logbackClassicVersion = '1.5.6'
        jcommanderVersion = '1.83'
        jacksonVersion = '2.17.1'
        jakartaAnnotationApiVersion = '3.0.0'
        junitJupiterVersion = '5.8.1'
        assertjVersion = '3.25.3'
    }
}
plugins {
    id 'java'
    id "org.openapi.generator" version "7.5.0"
}

openApiGenerate {
    generatorName = "java"
    library = "native"
    inputSpec = "$rootDir/src/main/resources/schema/scratch-game-configuration.yaml"
    outputDir = "${layout.buildDirectory.get().asFile}/generated"
    modelPackage = "org.scratchgame.model"
    configOptions = [
            interfaceOnly                 : "true",
            serializationLibrary          : "jackson",
            useJakartaEe                  : "true",
            additionalModelTypeAnnotations: "@lombok.AllArgsConstructor @com.fasterxml.jackson.annotation.JsonInclude(com.fasterxml.jackson.annotation.JsonInclude.Include.NON_NULL)",
    ]
    globalProperties = [
            models    : "",
            modelTests: "false",
            modelDocs : "false",
            apis      : "false",
    ]
}
sourceSets.main.java.srcDir "${layout.buildDirectory.get().asFile}/generated/src/main/java"
compileJava.dependsOn tasks.openApiGenerate

jar {
    manifest {
        attributes(
                'Main-Class': 'org.scratchgame.Main'
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

group 'org.scratchgame'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jApiVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackClassicVersion}"

    implementation "org.jcommander:jcommander:${jcommanderVersion}"

    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "jakarta.annotation:jakarta.annotation-api:${jakartaAnnotationApiVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
}

test {
    useJUnitPlatform()
}